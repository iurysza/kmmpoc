apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // The aar or jar and their sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'A Kotlin library that provides Brazilian vaccination data'
                    url = 'https://github.com/iurysza/kmmpoc'
                    licenses {
                        license {
                            name = 'Vaccination Tracker Licence'
                            url = 'https://github.com/iurysza/kmmpoc/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'iurysza'
                            name = 'Iury Souza'
                            email = 'iurysza@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/iurysza/kmmpoc.git'
                        developerConnection = 'scm:git:ssh://github.com/iurysza/kmmpoc.git'
                        url = 'https://github.com/iurysza/kmmpoc/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}